program = *function-def

function-def = "fn" identifier "(" [param-list] ")" "->" type-name block

param-list = param *("," param)
param = identifier ":" type-name

type-name = "i64"

block = "{" *statement [expression] "}"

statement = expr-stmt / let-stmt
expr-stmt = expression ";"
let-stmt = "let" identifier ":" type-name "=" expression ";"

expression = return-expr / assign-expr

return-expr = "return" [expression]

assign-expr = bitwise-or-expr [assign-op assign-expr]
assign-op = "="

bitwise-or-expr = bitwise-and-expr *(bitwise-or-op bitwise-and-expr)
bitwise-or-op = "|"

bitwise-and-expr = equality-expr *(bitwise-and-op equality-expr)
bitwise-and-op = "&"

equality-expr = relational-expr *(equality-op relational-expr)
equality-op = "==" / "!="

relational-expr = additive-expr *(relational-op additive-expr)
relational-op = "<" / "<=" / ">" / ">="

additive-expr = multiplicative-expr *(additive-op multiplicative-expr)
additive-op = "+" / "-"

multiplicative-expr = unary-expr *(multiplicative-op unary-expr)
multiplicative-op = "*" / "/" / "%"

unary-expr = unary-op unary-expr / primary-expr
unary-op = "-"

primary-expr = literal / identifier / if-expr / block / "(" expression ")"

if-expr = "if" expression block ["else" (if-expr / block)]

literal = integer
integer = 1*DIGIT

identifier = ALPHA *(ALPHA / DIGIT)
