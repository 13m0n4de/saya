program = *(const-def / static-def / function-def)

const-def = "const" identifier ":" type-name "=" expression ";"

static-def = "static" identifier ":" type-name "=" expression ";"

function-def = "fn" identifier "(" [param-list] ")" "->" type-name block

param-list = param *("," param)
param = identifier ":" type-name

type-name = base-type / array-type

base-type = "i64" / "str" / "bool"

array-type = "[" type-name ";" integer "]"

block = "{" *statement [expression] "}"

statement = expr-stmt / let-stmt
expr-stmt = expression ";"
let-stmt = "let" identifier ":" type-name "=" expression ";"

expression = return-expr / assign-expr

return-expr = "return" [expression]

assign-expr = logical-or-expr [assign-op assign-expr]
assign-op = "="

logical-or-expr = logical-and-expr *(logical-or-op logical-and-expr)
logical-or-op = "||"

logical-and-expr = bitwise-or-expr *(logical-and-op bitwise-or-expr)
logical-and-op = "&&"

bitwise-or-expr = bitwise-and-expr *(bitwise-or-op bitwise-and-expr)
bitwise-or-op = "|"

bitwise-and-expr = equality-expr *(bitwise-and-op equality-expr)
bitwise-and-op = "&"

equality-expr = relational-expr *(equality-op relational-expr)
equality-op = "==" / "!="

relational-expr = additive-expr *(relational-op additive-expr)
relational-op = "<" / "<=" / ">" / ">="

additive-expr = multiplicative-expr *(additive-op multiplicative-expr)
additive-op = "+" / "-"

multiplicative-expr = unary-expr *(multiplicative-op unary-expr)
multiplicative-op = "*" / "/" / "%"

unary-expr = unary-op unary-expr / postfix-expr
unary-op = "-"

postfix-expr = primary-expr *postfix-op
postfix-op = index-op / call-op

index-op = "[" expression "]"
call-op = "(" [expression *("," expression)] ")"

primary-expr = literal / identifier / array-expr
             / if-expr / while-expr / break-expr / continue-expr
             / block / "(" expression ")"

if-expr = "if" expression block ["else" (if-expr / block)]
while-expr = "while" expression block
break-expr = "break"
continue-expr = "continue"

array-expr = "[" [array-list / array-repeat] "]"
array-list = expression *("," expression)
array-repeat = expression ";" expression

literal = integer / string
integer = 1*DIGIT
string = DQUOTE *string-char DQUOTE
string-char = unescaped-char / escape-sequence
unescaped-char = %x20-21 / %x23-5B / %x5D-7E
escape-sequence = "\" escape-code
escape-code = DQUOTE / "\" / "n" / "r" / "t" / "0"

identifier = (ALPHA / "_") *(ALPHA / "_" / DIGIT)
