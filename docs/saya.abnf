program = *(const-def / function-def)

const-def = "const" identifier ":" type-name "=" expression ";"

static-def = "static" identifer ":" type-name "=" expression ";"

function-def = "fn" identifier "(" [param-list] ")" "->" type-name block

param-list = param *("," param)
param = identifier ":" type-name

type-name = "i64"

block = "{" *statement [expression] "}"

statement = expr-stmt / let-stmt
expr-stmt = expression ";"
let-stmt = "let" identifier ":" type-name "=" expression ";"

expression = return-expr / assign-expr

return-expr = "return" [expression]

assign-expr = logical-or-expr [assign-op assign-expr]
assign-op = "="

logical-or-expr = logical-and-expr *(logical-or-op logical-and-expr)
logical-or-op = "||"

logical-and-expr = bitwise-or-expr *(logical-and-op bitwise-or-expr)
logical-and-op = "&&"

bitwise-or-expr = bitwise-and-expr *(bitwise-or-op bitwise-and-expr)
bitwise-or-op = "|"

bitwise-and-expr = equality-expr *(bitwise-and-op equality-expr)
bitwise-and-op = "&"

equality-expr = relational-expr *(equality-op relational-expr)
equality-op = "==" / "!="

relational-expr = additive-expr *(relational-op additive-expr)
relational-op = "<" / "<=" / ">" / ">="

additive-expr = multiplicative-expr *(additive-op multiplicative-expr)
additive-op = "+" / "-"

multiplicative-expr = unary-expr *(multiplicative-op unary-expr)
multiplicative-op = "*" / "/" / "%"

unary-expr = unary-op unary-expr / primary-expr
unary-op = "-"

primary-expr = literal / call-expr / identifier
             / if-expr / while-expr / break-expr / continue-expr
             / block / "(" expression ")"

call-expr = identifier "(" [arg-list] ")"
arg-list = expression *("," expression)

if-expr = "if" expression block ["else" (if-expr / block)]
while-expr = "while" expression block
break-expr = "break"
continue-expr = "continue"

literal = integer
integer = 1*DIGIT

identifier = ALPHA *(ALPHA / DIGIT)
